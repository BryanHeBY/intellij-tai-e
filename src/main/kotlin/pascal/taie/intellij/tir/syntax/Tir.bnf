{
  parserClass="pascal.taie.intellij.tir.syntax.TirParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Tir"
  psiImplClassSuffix="Impl"
  psiPackage="pascal.taie.intellij.tir.syntax.psi"
  psiImplPackage="pascal.taie.intellij.tir.syntax.psi.impl"

  elementTypeHolderClass="pascal.taie.intellij.tir.syntax.TirTypes"
  elementTypeClass="pascal.taie.intellij.tir.syntax.TirElementType"
  tokenTypeClass="pascal.taie.intellij.tir.syntax.TirTokenType"

  psiImplUtilClass="pascal.taie.intellij.tir.syntax.TirPsiUtil"
}

tir_file ::= MODIFIER* (CLASS | INTERFACE) identifier_class class_extends? class_implements? LBRACE class_member* RBRACE

class_extends ::= EXTENDS identifier_class (COMMA identifier_class)*

class_implements ::= IMPLEMENTS identifier_class (COMMA identifier_class)*

class_member ::= field_def | method_def | COMMENT

// Field def
field_def ::= MODIFIER* identifier_type identifier_field_list SEMICOLON
// ---

// Method def
method_def ::= method_sig (LBRACE method_body RBRACE | SEMICOLON)

method_sig ::= MODIFIER* identifier_type identifier_method LPAREN (identifier_type IDENTIFIER (COMMA identifier_type IDENTIFIER)*)? RPAREN

method_body ::= (method_body_var | method_body_line | method_body_catch)+

method_body_var ::= identifier_type identifier_variable (COMMA identifier_variable)* SEMICOLON

method_body_line ::= LINE_NUMBER method_body_stmt SEMICOLON

method_body_catch ::= TRY LBRACKET INTEGER COMMA INTEGER RPAREN COMMA CATCH identifier_variable AT INTEGER

// copy stmts and more
method_body_stmt ::= lvalue EQUAL rvalue
                   | identifier_variable EQUAL NEW identifier_type
                   | identifier_variable EQUAL NEW_ARRAY identifier_type LBRACKET identifier_variable RBRACKET
                   | THROW identifier_type
                   | CATCH identifier_variable
                   | invoke_expr
                   | IF LPAREN (unary_expr | binary_expr | IDENTIFIER) RPAREN GOTO INTEGER
                   | GOTO INTEGER
                   | RETURN identifier_variable?
                   | NOP

lvalue ::= identifier_variable DOT field_sig
         | identifier_variable LBRACKET identifier_variable RBRACKET
         | identifier_variable

rvalue ::= identifier_variable DOT field_sig
         | identifier_variable LBRACKET identifier_variable RBRACKET
         | LPAREN identifier_type RPAREN identifier_variable
         | invoke_expr
         | unary_expr
         | binary_expr
         | identifier_variable INSTANCEOF identifier_type
         | identifier_variable
         | FLOAT
         | INTEGER
         | STRING_LITERAL

field_sig ::= LANGLE field_ref RANGLE

field_ref ::= IDENTIFIER COLON identifier_type IDENTIFIER

unary_expr ::= (ADD_OP | SUB_OP | NOT_OP) identifier_variable

binary_expr ::= identifier_variable (LANGLE | RANGLE | CMP_OP | BIT_OP | ADD_OP | SUB_OP | MUL_OP | DIV_OP | MOD_OP) identifier_variable

// invoke method
invoke_expr ::= INVOKE_KEY (identifier_variable DOT)? invoke_method_ref LPAREN invoke_argument_list? RPAREN

invoke_method_ref ::= LANGLE identifier_class COLON identifier_type identifier_method LPAREN (identifier_type (COMMA identifier_type)*)? RPAREN RANGLE

invoke_argument_list ::= identifier_variable (COMMA identifier_variable)*
// ---

// helpers
identifier_class ::= IDENTIFIER

identifier_field ::= IDENTIFIER

identifier_method ::= IDENTIFIER | LANGLE IDENTIFIER RANGLE

identifier_variable ::= IDENTIFIER | CONSTANT_IDENTIFIER  {
    mixin="pascal.taie.intellij.tir.syntax.TirNamedElementMixin"
    implements="pascal.taie.intellij.tir.syntax.TirNamedElement"
}

identifier_type ::= identifier_class (LBRACKET RBRACKET)? | NULL_TYPE

identifier_field_list ::= identifier_field (COMMA identifier_field)*
